plugins {
    id 'airship-module'
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

repositories {
    gradlePluginPortal()
    google()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

description = "Airship Android Chat extension."

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        // Exclude file to avoid
        // Error: Duplicate files during packaging of APK
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/*.kotlin_module'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    namespace 'com.urbanairship.chat'
}

kapt.includeCompileClasspath = false

dependencies {
    api project(':urbanairship-core')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$rootProject.kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.kotlinCoroutinesVersion"

    // AndroidX
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.androidxConstraintLayoutVersion"
    implementation "androidx.paging:paging-runtime:$rootProject.androidxPagingVersion"
    implementation "androidx.core:core:$rootProject.androidxCoreVersion"
    implementation "androidx.room:room-runtime:$rootProject.androidxRoomVersion"
    kapt "androidx.room:room-compiler:$rootProject.androidxRoomVersion"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okHttpVersion"

    // Support libraries
    implementation "androidx.appcompat:appcompat:$rootProject.androidxAppCompatVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // Tests
    testImplementation project(':urbanairship-test')
    testImplementation "junit:junit:$rootProject.junitVersion"
    testImplementation "androidx.test:core:$rootProject.androidxTestCoreVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectricVersion"
    testImplementation "androidx.test.ext:junit:$rootProject.androidxTestJunitVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.kotlinxCorutinesTest"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$rootProject.mockitoKotlinVersion"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
            }
        }
    }
}